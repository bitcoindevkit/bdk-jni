apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

task buildRust(type: Exec) {
    workingDir '../'
    commandLine './build-android.sh'
}

build.dependsOn buildRust

afterEvaluate {
    android.libraryVariants.all { variant ->
        variant.javaCompileProvider.get().dependsOn(buildRust)
    }

    publishing {
        publications {
            // create a Maven publication called "release"
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'org.bitcoindevkit.bdkjni'
                artifactId = 'bdk-android'
                version = '0.2.1-dev'
            }

            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = 'org.bitcoindevkit.bdkjni'
                artifactId = 'bdk-android-debug'
                version = '0.2.1-dev'
            }
        }
    }
}

dependencies {
    api(project(':jvm'))
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'com.github.tony19:logback-android:2.0.0'
    androidTestImplementation(project(':test-fixtures'))
}
